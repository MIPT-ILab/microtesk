address VA(value: 16)
address PA(value: 14)

segment DIRECT (va: VA) = (pa: PA)
  range = (0xc0000000, 0xffffffff)
  read = {
    pa.value = va.value<13..0>;
  }

memory buffer PageTable(va: VA)
  ways = 1
  sets = 2 ** 6
  entry = (vpn: 6, pfn: 6, unused: 4)
  index = va.value<13..8>
  match = va.value<13..8> == vpn

buffer TLB(va: VA)
  ways = 4
  sets = 1
  entry = (vpn: 6, pfn: 6)
  index = 0
  match = va.value<13..8> == vpn
  policy = FIFO

segment MAPPED (va: VA) = (pa: PA)
  range = (0x00000000, 0x3fffffff)

var tlbEntry: TLB.entry;
var pageTableVa: VA;
var pageTableEntry: PageTable.entry;

  read = {
    if (TLB(va).hit) then
      tlbEntry = TLB(va);
    else
      pageTableVa.value = 0;
      pageTableVa.value<15..14> = 0b11;
      pageTableVa.value<6..1> = va.value<13..8>;

      pageTableEntry = PageTable(pageTableVa);

      if (pageTableEntry.vpn != va.value<13..8>) then
        exception("AddressError");
      endif;

      tlbEntry.vpn = pageTableEntry.vpn;
      tlbEntry.pfn = pageTableEntry.pfn;
      TLB(va) = tlbEntry;
    endif;

    pa.value<13..8> = tlbEntry.pfn;
    pa.value<7..0> = va.value<7..0>;
  }

function TranslateAddress(va: VA): PA
var pa: PA;
{
  if (va.value<0> != 0) then
    exception("AddressError");
  endif;

  if (DIRECT(va).hit) then
    pa = DIRECT(va);
  elif (MAPPED(va).hit) then
    pa = MAPPED(va);
  else
    exception("AddressError");
  endif;

  return pa;
}

buffer MEM (pa: PA)
  ways  = 1
  sets  = 2 ** 13
  entry = (data: 16)
  index = pa.value<13..1>
  match = 0

mmu vmem (va: VA) = (data: 16)
  var pa: PA;
  var memEntry: MEM.entry;

  read = {
    pa = TranslateAddress(va);
    memEntry = MEM(pa);
    data = memEntry.data;
  }

  write = {
    pa = TranslateAddress(va);
    memEntry.data = data;
    MEM(pa) = memEntry;
  }
